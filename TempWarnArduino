//Chris Fink - 08/23/14
//ECEC 304 - Final Project
//Temperature Warning System

//Creates a temperature warning system that communicates with...
//Matlab across the serial

#include <Wire.h>
int tmp102Address = 0x48; //Address 72 for tmp102 ADD0 pin to gnd

const int buzz = 9; //PINS
const int ledG = 13;
const int ledR = 12;
const byte high = 255; //LED brightness
const byte off = 0;
const int setTemp = 27; //Max Temp; Set alarm
int mode = -1; //Intialize variable for serial read
float celsius; //Variable for the getTemperature function

void setup(){
  //Setup Serial, Initialize I2C and pins
  Serial.begin(9600);
  Wire.begin();
  pinMode(buzz, OUTPUT);//Alarm
  pinMode(ledG, OUTPUT);//Green LED
  pinMode(ledR, OUTPUT);//Red LED
  
  //Confirm serial connection with MATLAB
  Serial.println('a');
  char a = 'b';
  while(a!= 'a'){
    a = Serial.read();//Wait for Matlab to send 'a'
  }
  Serial.println(setTemp);//Tell Matlab the maxTemp
}

void loop(){
  //Receive 'T' from Matlab then send temperature to serial 
  if(Serial.available() > 0){
    mode = Serial.read();
    switch(mode){
      case 'T':
      getTemperature();
      Serial.println(celsius);
      break;
    }
  }
  //Check current temp, alter LEDs accordingly
  if(celsius < setTemp){
   digitalWrite(ledR,off);
   digitalWrite(ledG,high);
  }
  if(celsius > setTemp){
    digitalWrite(ledG,off);
    digitalWrite(ledR,high);
    beeper(20);//Call Beeper function, use 20ms delay
  }
  
}

//Beeper alarm tone function
void beeper(unsigned char stall){
  tone(9,15000);
  delay(stall);
  noTone(9);
}

//Temperature function
void getTemperature(){
Wire.requestFrom(tmp102Address,2); //Get two bit reading from tmp102

byte MSB = Wire.read(); //Most significant bit
byte LSB = Wire.read(); //Least significant bit

int Temp = ((MSB<<8) | LSB)>>4; //Shift MSB left 8, add LSB to the first 8 bits in MSB
                                //Shift sum right 4 bits to obtain 12 total bits
                                
celsius = Temp*0.0625; //Celsius conversion

}


